moai_set_local_source_dir()

add_definitions(-DGLUTHOST_USE_UNTZ)

# Libraries to link
set(hosts_lib_list
    aku
    moaicore
    utf8
    jansson
    ${OPENGL_LIBRARIES}
    ${GLUT_LIBRARIES}
    GLEW
)

# Update include directories to ignore supplied bytecode
include_directories(BEFORE "${MOAI_BINARY_DIR}/bogus")

# Build intermediate executable to compile lua bytecode for embedding
add_custom_command(
    OUTPUT "${MOAI_BINARY_DIR}/bogus/lua-headers/moai_lua.h"
    COMMAND
        "${CMAKE_CURRENT_SOURCE_DIR}/genbogus.sh"
        "${MOAI_BINARY_DIR}/bogus/lua-headers/moai_lua.h"
)
add_executable(bytecode-compiler
    "${local_source_dir}/GlutHost.cpp"
    "${local_source_dir}/GlutHostMain.cpp"
    "${MOAI_BINARY_DIR}/bogus/lua-headers/moai_lua.h"
)
target_link_libraries(bytecode-compiler ${hosts_lib_list})

# Use intermediate executable to compile lua bytecode for embedding
add_custom_command(
    OUTPUT "${MOAI_BINARY_DIR}/src/lua-headers/moai_lua.h"
    COMMAND
        "${CMAKE_CURRENT_SOURCE_DIR}/genreal.sh"
        "${CMAKE_CURRENT_BINARY_DIR}/bytecode-compiler"
        "${MOAI_SOURCE_DIR}/src/lua-headers/main.lua"
        "${MOAI_SOURCE_DIR}/src/lua-headers/moai.lua"
        "${MOAI_BINARY_DIR}/src/lua-headers/moai_lua.h"
    DEPENDS
        bytecode-compiler
        "${MOAI_SOURCE_DIR}/src/lua-headers/main.lua"
        "${MOAI_SOURCE_DIR}/src/lua-headers/moai.lua"
)

# Update include directories to favor the generated bytecode
include_directories(BEFORE "${MOAI_BINARY_DIR}/src")

# Build main executable
add_executable(moai-untz
    "${local_source_dir}/GlutHost.cpp"
    "${local_source_dir}/GlutHostMain.cpp"
    "${MOAI_BINARY_DIR}/src/lua-headers/moai_lua.h"
)

target_link_libraries(moai-untz ${hosts_lib_list})
